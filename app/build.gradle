apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def signConfigFile = project.rootProject.file('local.properties')
def hasSignConfig = false
def localProps = new Properties()

if (signConfigFile.exists()) {
    localProps.load(project.rootProject.file(signConfigFile).newInputStream())
    hasSignConfig = localProps['keystore.storeFile'] != null
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.eye.cool.ridding"
        targetSdkVersion rootProject.ext.targetSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion
        versionCode 2
        versionName "1.0.2"
    }

    signingConfigs {
        release {
            if (hasSignConfig) {
                storeFile file(localProps['keystore.storeFile'])
                storePassword localProps['keystore.storePassword']
                keyAlias localProps['keystore.keyAlias']
                keyPassword localProps['keystore.keyPassword']
            }
        }
        debug {
            storeFile file(project.rootProject.file('debug.keystore'))
            storePassword 'android'
            keyAlias 'alias'
            keyPassword 'android'
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            useProguard false
        }

        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            minifyEnabled true
            useProguard true
        }
    }

    useLibrary 'org.apache.http.legacy'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:recyclerview-v7:28.0.0'

    //如果你想应用能够兼容Android6.0，请添加此依赖(org.apache.http.legacy.jar)
    implementation 'cn.bmob.android:http-legacy:1.0'
    implementation 'cn.bmob.android:bmob-sdk:3.5.0'

    implementation 'com.github.wshychbydh:photo:1.2.2'
    implementation project(':scan')
}
repositories {
    mavenCentral()
}
